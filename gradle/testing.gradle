// Use TestNG framework instead of default JUnit
test {
	// Rearrange test classpath, add compiled JAR instead of main classes directory
	classpath = files(jar.archiveFile) + sourceSets.test.output + configurations.testRuntimeClasspath
	
	useTestNG() {
		suites "src/test/resources/unit-test.xml"
	}
	
	testLogging {
		exceptionFormat "full"
		events "passed", "skipped", "failed"
	}
}

// Dedicated task to run test262
task test262(type: Test, dependsOn: testClasses, group: "verification") {
	description "Runs official ECMAScript conformance test."
	mustRunAfter test
	
	// Rearrange test classpath, add compiled JAR instead of main classes directory
	classpath = files(jar.archiveFile) + sourceSets.test.output + configurations.testRuntimeClasspath
	
	useTestNG() {
		suites "src/test/resources/test262.xml"
	}
	
	testLogging {
		showStandardStreams = true
	}
}

// For coveralls check jacoco xml report should be turned on
jacocoTestReport {
	dependsOn test
	reports {
        xml.required = true
    }
}